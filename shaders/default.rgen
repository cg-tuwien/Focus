#version 460
#extension GL_NV_ray_tracing : require

struct RayTracingHit {
	vec4 color;
	vec4 transparentColor[2];
	float transparentDist[4];	//0 = goal, 1 = character
	uvec4 various;		//x = goal, y = recursions, z = renderCharacter
};

layout(push_constant) uniform PushConstants {
	mat4 viewMatrix;
} pushConstants;

layout(set = 2, binding = 0) uniform accelerationStructureNV topLevelAS;
layout(set = 1, binding = 0, rgba8) uniform image2D image;

layout(set = 4, binding = 0) buffer FocusHit {
	uint foundHit;
};

layout(location = 0) rayPayloadNV RayTracingHit hitValue;

void main() 
{
    const vec2 pixelCenter = vec2(gl_LaunchIDNV.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeNV.xy);

    vec2 d = inUV * 2.0 - 1.0;
    float aspectRatio = float(gl_LaunchSizeNV.x) / float(gl_LaunchSizeNV.y);
	
    vec3 origin = vec3(0, 0, 0.0);
    vec3 direction = normalize(vec3(d.x * aspectRatio, d.y, sqrt(3))); // 1 => sqrt(3) is the scaling factor from a fov of 90 to 60
	vec4 p1 = vec4(origin, 1.0);
	vec4 p2 = vec4(origin + direction, 1.0);
	mat4 cameraTransform = inverse(pushConstants.viewMatrix);
	vec4 vp1 = cameraTransform * p1;
	vec4 vp2 = cameraTransform * p2;
	origin = vec3(vp1);
	direction = vec3(normalize(vp2 - vp1));

    uint rayFlags = gl_RayFlagsNoneNV;
    uint cullMask = 0xff;
    float tmin = 0.001;
    float tmax = 100.0;
	hitValue.color = vec4(0);
	hitValue.transparentColor[0] = vec4(0);
	hitValue.transparentColor[1] = vec4(0);
	hitValue.transparentDist[0] = 200.0;
	hitValue.transparentDist[1] = 200.0;
	hitValue.various = uvec4(0, 4, 0, 0);
    traceNV(topLevelAS, rayFlags, cullMask, 0 /*sbtRecordOffset*/, 0 /*sbtRecordStride*/, 0 /*missIndex*/, origin, tmin, direction, tmax, 0 /*payload*/);

	if (abs(d.x) < 0.2 && abs(d.y) < 0.2*aspectRatio) {
		atomicAdd(foundHit, hitValue.various.x);
			if (abs(abs(d.x)-0.2) < 0.001 || abs(abs(d.y)-0.2*aspectRatio) < 0.001*aspectRatio) {
				hitValue.color.rgb -= vec3(0.1);
			}
			if (abs(d.x) < 0.05 && abs(d.y) < 0.001*aspectRatio || abs(d.y) < 0.05*aspectRatio && abs(d.x) < 0.001) {
				hitValue.color.rgb += vec3(0.2);
			}
	}

	//hitValue.color.rgb = (1-options.winval)*hitValue.color.rgb + options.winval*vec3(1,1,0.5);

    imageStore(image, ivec2(gl_LaunchIDNV.xy), vec4(hitValue.color.rgb, 0.0));
}
